//@version=5

//Made by @HarryCTC

indicator('Support & Resistance Zones', overlay=true)

fractalPeriods = input.int(title='Switch Zone Period', defval=3, minval=1)
zoneCount = input.int(title="No. of Displayed Zones", defval=2, minval=1, maxval=8, step=1)
zoneExtension = input.int(title="Zone Extension", defval=20, minval=1, step=1)
resistanceColor = input.color(title="Resistance Zone Color", defval=color.blue)
supportColor = input.color(title="Support Zone Color", defval=color.gray)
borderColor = input.color(title="Zone Border Color", defval=color.rgb(102, 105, 114))

bullFractal = ta.pivothigh(fractalPeriods, fractalPeriods)
bearFractal = ta.pivotlow(fractalPeriods, fractalPeriods)

var last4BullFractals = array.new_box()
var last4BearFractals = array.new_box()

// Draw Resistance Zone box
if bullFractal
    boxId = box.new(na, na, na, na)
    if close[fractalPeriods] > open[fractalPeriods] // Bull candle
        boxId := box.new(top=high[fractalPeriods], bottom=close[fractalPeriods], left=bar_index - fractalPeriods - 2, right=bar_index + zoneExtension, border_color=borderColor, border_width=1, bgcolor=color.new(resistanceColor, 39))
    else // Bear candle
        boxId := box.new(top=high[fractalPeriods], bottom=open[fractalPeriods], left=bar_index - fractalPeriods - 2, right=bar_index + zoneExtension, border_color=borderColor, border_width=1, bgcolor=color.new(resistanceColor, 39))

    if array.size(last4BullFractals) >= zoneCount
        box.delete(array.get(last4BullFractals, 0))
        array.shift(last4BullFractals)
    array.push(last4BullFractals, boxId)

// Draw Support Zone box
if bearFractal
    boxId = box.new(na, na, na, na)
    if close[fractalPeriods] > open[fractalPeriods] // Bull candle
        boxId := box.new(top=open[fractalPeriods], bottom=low[fractalPeriods], left=bar_index - fractalPeriods - 2, right=bar_index + zoneExtension, border_color=borderColor, border_width=1, bgcolor=color.new(supportColor, 39))
    else // Bear candle
        boxId := box.new(top=close[fractalPeriods], bottom=low[fractalPeriods], left=bar_index - fractalPeriods - 2, right=bar_index + zoneExtension, border_color=borderColor, border_width=1, bgcolor=color.new(supportColor, 39))

    if array.size(last4BearFractals) >= zoneCount
        box.delete(array.get(last4BearFractals, 0))
        array.shift(last4BearFractals)
    array.push(last4BearFractals, boxId)

// Update the right side of all boxes
if barstate.islast
    for i = 0 to array.size(last4BullFractals) - 1
        box.set_right(array.get(last4BullFractals, i), bar_index + zoneExtension)
    for i = 0 to array.size(last4BearFractals) - 1
        box.set_right(array.get(last4BearFractals, i), bar_index + zoneExtension)
